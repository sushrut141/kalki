cmake_minimum_required(VERSION 3.23)
project(kalki LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

option(KALKI_ENABLE_ZSTD "Enable Zstd compression" ON)
option(KALKI_BUILD_TESTS "Build tests" ON)

find_package(absl CONFIG REQUIRED)
find_package(Protobuf CONFIG REQUIRED)
find_package(gRPC CONFIG REQUIRED)
find_package(CURL REQUIRED)
find_package(SQLite3 REQUIRED)
find_package(Faiss CONFIG QUIET)
if(NOT Faiss_FOUND)
  find_package(PkgConfig REQUIRED)
  pkg_check_modules(FAISS REQUIRED IMPORTED_TARGET faiss)
  add_library(Faiss::Faiss INTERFACE IMPORTED)
  target_link_libraries(Faiss::Faiss INTERFACE PkgConfig::FAISS)
elseif(TARGET faiss AND NOT TARGET Faiss::Faiss)
  add_library(Faiss::Faiss ALIAS faiss)
elseif(TARGET faiss::faiss AND NOT TARGET Faiss::Faiss)
  add_library(Faiss::Faiss ALIAS faiss::faiss)
elseif(NOT TARGET Faiss::Faiss)
  message(FATAL_ERROR
          "FAISS was found, but no supported CMake target was exported "
          "(expected Faiss::Faiss, faiss, or faiss::faiss).")
endif()

if(KALKI_ENABLE_ZSTD)
  find_package(PkgConfig QUIET)
  if(PkgConfig_FOUND)
    pkg_check_modules(ZSTD libzstd)
  endif()
endif()

add_subdirectory(proto)
add_subdirectory(src/common)
add_subdirectory(src/storage)
add_subdirectory(src/metadata)
add_subdirectory(src/llm)
add_subdirectory(src/workers)
add_subdirectory(src/query)
add_subdirectory(src/core)
add_subdirectory(src/api)
add_subdirectory(src/server)

if(KALKI_BUILD_TESTS)
  enable_testing()
  add_subdirectory(tests)
endif()
